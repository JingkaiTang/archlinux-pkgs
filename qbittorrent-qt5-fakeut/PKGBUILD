# Maintainer: Aetf
# Contributor: anex
# Contributor: Sevenseven
# Contributor: Ner0

pkgname=qbittorrent-qt5-git
pkgver=3.3.7.7808
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt5 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar>=1.0.0' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'boost' 'qt5-tools')
optdepends=('python: needed for search')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=qbittorrent.install
source=('git://github.com/qbittorrent/qBittorrent.git')
sha256sums=('SKIP')

pkgver() {
    cd qBittorrent
    #echo $(cat version.pri | grep -o '[0-9]..*[0-9]').$(git rev-list --count master)
    awk '/^VER_MAJOR/{X=$NF}/^VER_MINOR/{Y=$NF}/^VER_BUGFIX/{Z=$NF}END{printf "%d.%d.%d.",X, Y, Z}'\
        version.pri
    git rev-list --count master
}

prepare() {
  cd qBittorrent
  git checkout release-3.3.7
  sed -i 's/\(static const char PEER_ID\[\] = "\)qB\(";\)/\1UT\2/' src/base/bittorrent/session.cpp
  sed -i 's/\(libt::fingerprint fingerprint(PEER_ID, \)VERSION_MAJOR, VERSION_MINOR, VERSION_BUGFIX, VERSION_BUILD\();\)/\13, 3, 0, 0\2/' src/base/bittorrent/session.cpp
  sed -i 's/\(sessionSettings.user_agent = \)"qBittorrent " VERSION\(;\)/\1"uTorrent\/3300(29667)"\2/' src/base/bittorrent/session.cpp
}

build() {
  cd qBittorrent
  ./configure --prefix=/usr \
              --with-qt5
  make
}

package() {
  cd qBittorrent
  make INSTALL_ROOT="$pkgdir/" install
}

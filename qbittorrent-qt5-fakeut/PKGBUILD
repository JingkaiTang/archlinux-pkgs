# Maintainer: Jingkai Tang
# Contributor: Aetf
# Contributor: anex
# Contributor: Sevenseven
# Contributor: Ner0

pkgname=qbittorrent-qt5-fakeut
pkgver=3.3.7.8168
pkgrel=1
git_tag="release-3.3.7"
pkgdesc="A bittorrent client written in C++ / Qt5 using the good libtorrent library, patch with Î¼torrent user agent"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar>=1.0.0' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'boost' 'qt5-tools')
optdepends=('python: needed for search')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=qbittorrent.install
source=('git://github.com/qbittorrent/qBittorrent.git')
sha256sums=('SKIP')

pkgver() {
    cd qBittorrent
    awk '/^VER_MAJOR/{X=$NF}/^VER_MINOR/{Y=$NF}/^VER_BUGFIX/{Z=$NF}END{printf "%d.%d.%d.",X, Y, Z}'\
        version.pri
    git rev-list --count master
}

prepare() {
  cd qBittorrent
  git checkout $git_tag
  sed -i 's/^.*static const char PEER_ID.*$/static const char PEER_ID[] = "UT";/' src/base/bittorrent/session.cpp
  sed -i 's/^.*libt::fingerprint fingerprint.*$/libt::fingerprint fingerprint(PEER_ID, 3, 3, 0, 0);/' src/base/bittorrent/session.cpp
  sed -i 's/^.*sessionSettings.user_agent =.*$/sessionSettings.user_agent = "uTorrent\/3300(29667)";/' src/base/bittorrent/session.cpp
}

build() {
  cd qBittorrent
  ./configure --prefix=/usr \
              --with-qt5
  make
}

package() {
  cd qBittorrent
  make INSTALL_ROOT="$pkgdir/" install
}

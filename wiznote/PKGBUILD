# $Id: PKGBUILD 153763 2015-12-20 11:01:56Z fyan $
# Maintainer: Jingkai Tang <jingkaitang@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Albert.Zhou <albert.zhou@wiz.cn>

pkgname=wiznote
pkgver=2.3.3.20151218
pkgrel=1
pkgdesc="Opensource cross-platform cloud based note-taking client"
arch=('i686' 'x86_64')
url="http://www.wiznote.com"
license=('GPL3' 'custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'qt5-base' 'qt5-webkit' 'crypto++') # indirect: qt5-declarative qt5-sensors zlib glic gcc-libs
# shared clucene/quazip triggers segfault
makedepends=('cmake' 'qt5-tools' 'git' 'boost')
install=wiznote.install
_wiznote_project_name="WizQTClient"
source=("git://github.com/WizTeam/${_wiznote_project_name}.git")
md5sums=('SKIP')

prepare() {
  sed -e 's/add_subdirectory(cryptopp)//' \
      -i $_wiznote_project_name/lib/CMakeLists.txt
  
  sed -e 's/cryptlib/cryptopp/' \
      -i $_wiznote_project_name/src/CMakeLists.txt

  rm -rf build; mkdir build
}

build() {
  cd build

  cmake \
    -DWIZNOTE_USE_QT5=YES \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../$_wiznote_project_name
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
